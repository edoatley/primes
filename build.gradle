plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version "2.7.5"
	id 'io.spring.dependency-management' version '1.1.0'
	id 'io.freefair.lombok' version '6.6.2'
	id 'info.solidsoft.pitest' version '1.9.0'
	id "me.champeau.jmh" version "0.6.8"
	id "com.github.joselion.pretty-jupiter" version "1.7.0"
}

group = 'com.edoatley'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-webflux"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation 'io.projectreactor:reactor-test:3.5.0'
}

tasks.withType(Test) {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test
	sourceSets sourceSets.main
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
	reports {
		xml.enabled true
		html.enabled true
		xml.destination file("${buildDir}/reports/jacoco/jacoco.xml")
		html.destination file("${buildDir}/reports/jacoco/html")
	}
}

pitest {
	threads = 4
	outputFormats = ['XML', 'HTML']
	junit5PluginVersion = '0.12'
	timestampedReports = false
	reportDir=file("${buildDir}/reports/pitest/html")
}

test.finalizedBy 'jacocoTestReport'

jmh {
	warmupIterations = 2
	iterations = 5
	fork = 1
}